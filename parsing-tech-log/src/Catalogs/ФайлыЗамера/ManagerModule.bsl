Функция ПолучитьФайлПоПолномуИмени(ЗНАЧ Замер, ЗНАЧ ПолноеИмяФайла) Экспорт
	ТипЗамера = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Замер,"ТипЗамера");
	ПолноеИмяМассив = СтрРазделить(ПолноеИмяФайла, "\");
	Если ПолноеИмяМассив.Количество()>1 Тогда
		ПоследняяЧасть = ПолноеИмяМассив[ПолноеИмяМассив.ВГраница()-1] + "\" + ПолноеИмяМассив[ПолноеИмяМассив.ВГраница()];
	Иначе
		ПоследняяЧасть = ПолноеИмяФайла;
	КонецЕсли;
	ИмяБезРасширения = "";
	Если НЕ ТипЗамера=ПредопределенноеЗначение("Перечисление.ТипыЗамеров.Произвольный") Тогда
		ИмяБезРасширения = Лев(ПолноеИмяМассив[ПолноеИмяМассив.ВГраница()],8);
		Если ТипЗамера=ПредопределенноеЗначение("Перечисление.ТипыЗамеров.PerfomanceMonitor") Тогда
			ИмяБезРасширения = Прав(ПолноеИмяМассив[ПолноеИмяМассив.ВГраница()],12);
			ИмяБезРасширения = Лев(ИмяБезРасширения,8);
		КонецЕсли;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ФайлыЗамера.Ссылка
	|ИЗ
	|	Справочник.ФайлыЗамера КАК ФайлыЗамера
	|ГДЕ
	|	ФайлыЗамера.Владелец = &Замер
	|	И ФайлыЗамера.Наименование = &Наименование";
	Запрос.УстановитьПараметр("Наименование", ПоследняяЧасть);
	Запрос.УстановитьПараметр("Замер", Замер);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ФайлОбъект = Справочники.ФайлыЗамера.СоздатьЭлемент();
		ФайлОбъект.Наименование = ПоследняяЧасть;
		ФайлОбъект.Владелец = Замер;
		ФайлОбъект.ДатаФайла = ?(ИмяБезРасширения="","",ОбновлениеДанныхРегламентное.ПолучитьПериодПоИмениФайла(ИмяБезРасширения));
		ФайлОбъект.Записать();
		Результат = ФайлОбъект.Ссылка;
	Иначе
		Результат = РезультатЗапроса.Выгрузить()[0].Ссылка;
	КонецЕсли;
	Возврат Результат;
КонецФункции