Функция ПолучитьСостояние(ЗНАЧ ФайлЗамера) Экспорт
	Результат = Новый Структура("ДатаИзмененияСостояния,ДатаПрочитанныхДанных,ТекущееСостояние,ПредыдущееСостояние,ОбработаноСтрок", Дата(1,1,1), Дата(1,1,1),"","",0);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	FSM.ТекущееСостояние КАК ТекущееСостояние,
	|	FSM.ПредыдущееСостояние КАК ПредыдущееСостояние,
	|	FSM.ОбработаноСтрок КАК ОбработаноСтрок,
	|	FSM.ДатаИзмененияСостояния КАК ДатаИзмененияСостояния,
	|	FSM.ДатаПрочитанныхДанных КАК ДатаПрочитанныхДанных
	|ИЗ
	|	РегистрСведений.СостояниеОбработкиFSM КАК FSM
	|ГДЕ
	|	FSM.ФайлЗамера = &ФайлЗамера";
	Запрос.УстановитьПараметр("ФайлЗамера", ФайлЗамера);
	РезультатЗапроса = Запрос.Выполнить(); 
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса.Выгрузить()[0]);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура УстановитьСостояние(
		ФайлЗамера, 
		ОбработаноСтрок,
		ДатаПрочитанныхДанных,
		ТекущееСостояние, 
		ПредыдущееСостояние) Экспорт
		
	ТекущееСостояниеFSM = ПолучитьСостояние(ФайлЗамера);
	Если ТекущееСостояниеFSM.ОбработаноСтрок = ОбработаноСтрок Тогда
		Возврат;
	КонецЕсли;	
		
	МенеджерЗаписи = РегистрыСведений.СостояниеОбработкиFSM.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ФайлЗамера 				= ФайлЗамера;
	МенеджерЗаписи.ТекущееСостояние 		= ТекущееСостояние;
	МенеджерЗаписи.ПредыдущееСостояние 		= ПредыдущееСостояние;
	МенеджерЗаписи.ДатаИзмененияСостояния 	= ТекущаяДата();
	МенеджерЗаписи.ДатаПрочитанныхДанных 	= ДатаПрочитанныхДанных;
	МенеджерЗаписи.ОбработаноСтрок 			= ОбработаноСтрок;
	МенеджерЗаписи.Записать();		
		
	
КонецПроцедуры