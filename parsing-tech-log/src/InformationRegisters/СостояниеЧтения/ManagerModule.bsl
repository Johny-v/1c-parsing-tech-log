Функция ПолучитьСостояние(ЗНАЧ ФайлЗамера) Экспорт
	Результат = Новый Структура("ПрочитаноСтрок,ЧтениеЗавершено,Размер,ДатаИзмененияСостояния,ДатаПрочитанныхДанных,Ошибка,ОписаниеОшибки",
		0, Ложь, 0, Дата(1,1,1), Дата(1,1,1), Ложь, "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СостояниеЧтения.Размер КАК Размер,
	               |	СостояниеЧтения.ЧтениеЗавершено КАК ЧтениеЗавершено,
	               |	СостояниеЧтения.ПрочитаноСтрок КАК ПрочитаноСтрок,
	               |	СостояниеЧтения.ДатаИзмененияСостояния КАК ДатаИзмененияСостояния,
	               |	СостояниеЧтения.ДатаПрочитанныхДанных КАК ДатаПрочитанныхДанных,
	               |	СостояниеЧтения.Ошибка КАК Ошибка,
	               |	СостояниеЧтения.ОписаниеОшибки КАК ОписаниеОшибки
	               |ИЗ
	               |	РегистрСведений.СостояниеЧтения КАК СостояниеЧтения
	               |ГДЕ
	               |	СостояниеЧтения.ФайлЗамера = &ФайлЗамера";
	Запрос.УстановитьПараметр("ФайлЗамера", ФайлЗамера);
	РезультатЗапроса = Запрос.Выполнить(); 
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса.Выгрузить()[0]);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура УстановитьСостояние(
		ФайлЗамера, 
		ПериодФайла,
		ПрочитаноСтрок, 
		ДатаНачалаЧтения,
		РазмерФайла,
		ДатаПрочитанныхДанных=Неопределено) Экспорт
		
	//если дата начала чтения больше чем конец часа (запас в 5 минут) 
	//считаем что файл больше дополняться не будет
	ЧтениеЗавершено = ДатаНачалаЧтения > КонецЧаса(ПериодФайла) + 300;
	
	ТекущееСостояние = ПолучитьСостояние(ФайлЗамера);
	Если ТекущееСостояние.ПрочитаноСтрок = ПрочитаноСтрок
		И ТекущееСостояние.ЧтениеЗавершено = ЧтениеЗавершено Тогда
		Возврат;
	КонецЕсли;	
	
	МенеджерЗаписи = РегистрыСведений.СостояниеЧтения.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ФайлЗамера 				= ФайлЗамера;
	МенеджерЗаписи.ПрочитаноСтрок 			= ПрочитаноСтрок;
	МенеджерЗаписи.ЧтениеЗавершено 			= ЧтениеЗавершено;
	МенеджерЗаписи.Размер 					= РазмерФайла;
	МенеджерЗаписи.ДатаИзмененияСостояния 	= ТекущаяДата();
	МенеджерЗаписи.ДатаПрочитанныхДанных 	= ДатаПрочитанныхДанных;
	МенеджерЗаписи.Ошибка = Ложь;
	МенеджерЗаписи.ОписаниеОшибки = "";
	МенеджерЗаписи.Записать();		
		
	
КонецПроцедуры

Процедура УстановитьСостояниеОшибки(ФайлЗамера, ОписаниеОшибки) Экспорт
	
	ТекущееСостояние = ПолучитьСостояние(ФайлЗамера);
	
	Если НЕ ТекущееСостояние.Ошибка = Истина Тогда
		МенеджерЗаписи = РегистрыСведений.СостояниеЧтения.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ТекущееСостояние);
		МенеджерЗаписи.ЧтениеЗавершено = Истина;
		МенеджерЗаписи.Ошибка = Истина;
		МенеджерЗаписи.ОписаниеОшибки = ОписаниеОшибки;
		МенеджерЗаписи.Записать();		
	КонецЕсли;
	
КонецПроцедуры

